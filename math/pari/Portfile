# -*- coding: utf-8; mode: tcl; tab-width: 4; indent-tabs-mode: nil; c-basic-offset: 4 -*- vim:fenc=utf-8:ft=tcl:et:sw=4:ts=4:sts=4

PortSystem          1.0
PortGroup           muniversal 1.0

name                pari
version             2.9.3
categories          math
platforms           darwin
license             GPL-2+
maintainers         {gmail.com:watsonbladd @wbl} openmaintainer

description         PARI/GP computer algebra system

long_description    PARI/GP is a widely used computer algebra system designed \
                    for fast  computations in number theory (factorizations, \
                    algebraic number theory,  elliptic curves...)

homepage            http://pari.math.u-bordeaux.fr/
master_sites        ${homepage}pub/pari/unix/

checksums           pari-${version}.tar.gz \
                    rmd160  4b8b4851d5b0e5d8e9fb5f9c919924200ede7426 \
                    sha256  e76a27779d2b1210ce1aba48363b98dd201a1bf876eb14f46ea6bd7769a00a63

depends_lib         port:gmp \
                    port:ncurses \
                    port:readline

post-extract {
    if {[file exists ${workpath}/data] && ![file exists ${worksrcpath}/data]} {
        file rename ${workpath}/data ${worksrcpath}/data
    }
}

platform darwin 8 {
    build.args-append EXTRADLLDFLAGS="-Wl,-single_module"
    destroot.args-append EXTRADLLDFLAGS="-Wl,-single_module"
    post-patch {
    # Update linker option names
    reinplace "s|-dylib_|-|g" \
        ${worksrcpath}/config/Makefile.SH \
        ${worksrcpath}/config/get_dlld
    }
}

array set arch_trans {
    i386    ix86
    x86_64  x86_64
    ppc     ppc
    ppc64   ppc64
}

if {[variant_isset universal]} {
    foreach arch [array names arch_trans] {
        set merger_configure_args($arch) \
            --host=$arch_trans($arch)-${os.platform}
    }
} else {
    configure.args-append --host=$arch_trans($build_arch)-${os.platform}
}

# llvm-gcc produces incorrect code, ticket #33024
compiler.blacklist  llvm-gcc-4.2

configure.cmd       ./Configure

# PARI/GP doesn't use autoconf, and if only *FLAGS environment variables are
# used, libreadline is not found. One needs *PATH environment variables.
configure.env       C_INCLUDE_PATH=${prefix}/include

# Always use GMP (forced by --with-gmp), as advised by the PARI developers:
#   http://www.math.u-bordeaux.fr/~belabas/pari/doc/faq.html#gnump
# There are no reasons not to use it. If a no_gmp variant is needed,
# one would need --without-gmp, as by default, PARI's Configure will
# choose GMP if it can find it.
configure.args      --mandir=${prefix}/share/man \
                    --with-gmp \
                    --with-readline \
                    --graphic=none
configure.universal_args-delete --disable-dependency-tracking

set merger_must_run_binaries yes

# pari does not build for ppc on intel
if {${build_arch} eq "i386" || ${build_arch} eq "x86_64"} {
    supported_archs i386 x86_64
} elseif {${build_arch} eq "ppc" || ${build_arch} eq "ppc64"} {
    supported_archs ppc ppc64
}
set universal_archs_supported ${supported_archs}

post-patch {
    # Do not build tex/dvi doc by default
    reinplace {s/\(doc all:\).*/\1/} \
        ${worksrcpath}/config/DOC_Make.SH
    reinplace {s/^\(install-doc: .*\) install-docdvi/\1/} \
        ${worksrcpath}/config/Makefile.SH
}

post-configure {
    # Pari's Configure sets the build directory according to --host,
    # but the config/objdir used by the top Makefile may tell otherwise
    if {[variant_isset universal]} {
        foreach arch [array names arch_trans] {
            if {![file isdirectory ${worksrcpath}-$arch]} continue
            set objdir [exec sh -c "cd ${worksrcpath}-$arch && config/objdir"]
            if {[file isdirectory \
                ${worksrcpath}-$arch/O${os.platform}-$arch_trans($arch)] && \
                ![file exists ${worksrcpath}-$arch/$objdir]} {
                ln -s O${os.platform}-$arch_trans($arch) \
                    ${worksrcpath}-$arch/$objdir
            }
        }
    } else {
        set objdir [exec sh -c "cd ${worksrcpath} && config/objdir"]
        if {[file isdirectory \
            ${worksrcpath}/O${os.platform}-$arch_trans($build_arch)] && \
            ![file exists ${worksrcpath}/$objdir]} {
            ln -s O${os.platform}-$arch_trans($build_arch) \
                ${worksrcpath}/$objdir
        }
    }
}

build.args-append   CXX=${configure.cxx}

build.target        gp

post-destroot {
    set docdir ${destroot}${prefix}/share/doc/${name}
    file mkdir ${docdir}
    xinstall -m 644 -v -W ${worksrcpath} \
        AUTHORS CHANGES COPYING ${docdir}
    ln -s ../../${name}/doc ${docdir}/doc
}

variant doc description {Build PDF documentation} {
    depends_build-append path:bin/tex:texlive
    build.target-append docpdf
    post-destroot {
        if {[variant_isset universal]} {
            set dir ${worksrcpath}-[lindex ${universal_archs_to_use} 0]
        } else {
            set dir ${worksrcpath}
        }
        xinstall -m 644 -W ${dir}/doc \
            develop.pdf libpari.pdf refcard.pdf tutorial.pdf users.pdf \
            ${destroot}${prefix}/share/${name}/doc
    }
}

# Qt support doesn't work with:
#   depends_lib-append port:qt4-mac
#   configure.args-delete --graphic=none
#   configure.args-append --graphic=Qt --with-qt=${prefix}
# Configure succeeds, but the build of src/graph/plotQt.c fails because
# ${prefix}/include/Qt is not in the include search path.

# Plotting works with X11, but the redraw after a window resize is
# sometimes incorrect.
variant x11 conflicts fltk description {Build with X11 support for the high-resolution plotting functions} {
    depends_lib-append port:xorg-libX11
    configure.args-delete --graphic=none
    configure.args-append --graphic=X11
}

variant elldata description {include precomputed elliptic curve (Q) data} {
    master_sites-append ${homepage}pub/pari/packages:elldata
    distfiles-append    elldata.tgz:elldata
    checksums-append    elldata.tgz \
                        md5     0ae49b7aa6a012ccd2804bfb831d3686 \
                        sha1    85f51ef85390f0f737fba10567d7d47f99249780 \
                        rmd160  e1a90bfaab3ec1db6698a9bb4c8700ec1a413497 \
                        sha256  6c909f09bb19c8a8125145b1aac37589[\
                                ]15daa7fd03530bc909dcb460d4eecb38
}

# Plotting works with fltk @1.1.10_3, but the GUI part freezes
# (a Force Quit is needed).
variant fltk conflicts x11 description {Build with FLTK support for the high-resolution plotting functions} {
    depends_lib-append path:lib/libfltk.dylib:fltk
    configure.args-delete --graphic=none
    configure.args-append --graphic=fltk
}

variant galdata description {include precomputed polynomial->Galois data} {
    master_sites-append ${homepage}pub/pari/packages:galdata
    distfiles-append    galdata.tgz:galdata
    checksums-append    galdata.tgz \
        md5     f9f61b2930757a785b568e5d307a7d75 \
        sha1    0fe78ed49e197066708a3fc3753b2651107a1264 \
        rmd160  488afbddf7026dd6d0c9ebbed72cb14c8c37173e \
        sha256  b7c1650099b24a20bdade47a85a92835[\
                ]1c586287f0d4c73933313873e63290dd
}

variant galpol description {include precomputed Galois->polynomial data} {
    master_sites-append ${homepage}pub/pari/packages:galpol
    distfiles-append    galpol.tgz:galpol
    checksums-append    galpol.tgz \
        md5     c241b3b40267f640df4d45ed2d604442 \
        sha1    5d21bba704a906b3149d1a7375f609e67445fc11 \
        rmd160  6015681df3415568392a68bfc05e1134411f2ca5 \
        sha256  484fba6e7bcef8e5c1cea0e1a75d74a3[\
                ]1122b3148a1301667d265427fb8db44d
}

variant mt description {Build multithreaded} {
    configure.args-append --mt=pthread
}

variant seadata description {include precomputed elliptic curve GF(q) data} {
    master_sites-append ${homepage}pub/pari/packages:seadata
    distfiles-append    seadata.tgz:seadata
    checksums-append    seadata.tgz \
        md5     6e9c119ccb3c65916a48e1a8cd899558 \
        sha1    fa3deb36df0ce71a466eb0ff0d4a18d48d44e8b9 \
        rmd160  e88637426f74c5223068fc2d916741c10d8279e4 \
        sha256  c9282a525ea3f92c1f9c6c69e37ac5a8[\
                ]7b48fb9ccd943cfd7c881a3851195833
}

variant tune description {tune speed parameters on the build machine} {
        configure.args-append --tune
}

default_variants    +elldata +galdata +galpol +seadata

livecheck.type      regex
livecheck.url       [lindex ${master_sites} 0]
livecheck.regex     ${name}-(\\d+\\.\\d+\\.\\d+)
